<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8" />
    <meta name="viewport" content="width=device-width, initial-scale=1.0"/>
    <title>HW 6 Ray Tracing</title>
    <style>
        body { 
            margin: 0; 
            background: #111; 
            color: #ccc;
            font-family: monospace;
            padding: 20px;
        }
        textarea {
            background: #222;
            color: #0f0;
            font: 14px monospace;
            width: 800px;
            height: 600px;
        }
        canvas {
            border: 1px solid #444;
        }
        .container {
            display: flex;
            gap: 20px;
            align-items: flex-start;
        }

        .canvas-container {
            display: flex;
            flex-direction: column;
        }

        .editor-panel {
            display: flex;
            flex-direction: column;
            gap: 10px;
        }
        .controls {
            margin-top: 10px;
        }
        .controls label {
            display: block;
            margin: 5px 0;
        }
    </style>
</head>
<body>

<h2>HW 6 Ray Tracing</h2>
<p>Edit vertex and fragment shaders on the left, and you can see the result on the right instantly. <br>
Please copy your edits to the source code if you want to save them, as the playground doesn't save them to the file.</p>

<div class="container">
    <div class="canvas-container">
        <canvas id="glCanvas" width="800" height="600"></canvas>
        <div class="controls">
            <label>
                Max Bounces: <span id="bouncesValue">4</span>
                <input type="range" id="bouncesSlider" min="1" max="8" value="4" step="1">
            </label>
            <label>
                Ambient Strength: <span id="ambientValue">0.25</span>
                <input type="range" id="ambientSlider" min="0" max="1" value="0.25" step="0.05">
            </label>
        </div>
    </div>
    <div class="editor-panel">
         <div>
            <b>Fragment Shader</b><br>
            <textarea id="fsEditor"></textarea>
        </div>
        <div>
            <b>Vertex Shader</b><br>
            <textarea id="vsEditor"></textarea>
        </div>
    </div>
</div>
    


<script id="vertex-shader" type="x-shader/x-vertex">#version 300 es
    in vec2 position;
    void main() {
        gl_Position = vec4(position, 0.0, 1.0);
    }
</script>

<script id="fragment-shader" type="x-shader/x-fragment">#version 300 es
    precision highp float;

    //time for animation
    uniform float u_time;
    uniform vec2 u_resolution;
    uniform int u_maxBounces;
    uniform float u_ambientStrength;

    out vec4 fragColor;

    // Material types
    const int MATERIAL_DIFFUSE = 0;
    const int MATERIAL_REFLECTIVE = 1;
    const int MATERIAL_REFRACTIVE = 2;

    // Ray structure
    struct Ray {
        vec3 origin;
        vec3 direction;
    };

    // Sphere structure
    struct Sphere {
        vec3 center;
        float radius;
        vec3 color;
        int material;
        float reflectivity;
        float refractiveIndex;
    };

    // Cube structure
    struct Cube {
        vec3 center;
        vec3 size;
        vec3 color;
        float reflectivity;
    };

    // Hit record structure
    struct HitData {
        bool hit;
        float t;
        vec3 point;
        vec3 normal;
        vec3 color;
        int material;
        float reflectivity;
        float refractiveIndex;
        bool frontFace;
    };

    // Scene objects
    Sphere spheres[2];
    Sphere lightSphere;
    Cube cube;

    // Initialize scene objects
    void initScene(float time) {
        // Light sphere - bright white light source
        lightSphere = Sphere(
            vec3(sin(time) * 2.0, 2.0, cos(time) * 2.0 - 1.0),
            0.3,
            vec3(10.0, 10.0, 8.0),
            MATERIAL_DIFFUSE,
            0.0,
            1.0
        );

        //cube - reflective mirror-like surface
        cube = Cube(
            vec3(0.0, 1.0, 1.0),
            vec3(2.0, 0.2, 2.0),
            vec3(0.9, 1.8, 0.9),
            0.8
        );

        // Sphere 1 - Glass-like refractive sphere
spheres[0] = Sphere(
    vec3(-1.0 + sin(time * 0.5) * 0.3, 0.0, -2.0),
    0.6,
    vec3(0.95, 0.95, 1.0),
    MATERIAL_REFRACTIVE,
    0.95,
    1.5
);

        // Sphere 2 - Diffuse colored sphere
        spheres[1] = Sphere(
            vec3(1.2 + cos(time * 0.7) * 0.3, -0.2, -1.5),
            0.5,
            vec3(0.8, 0.3, 0.3),
            MATERIAL_DIFFUSE,
            0.0,
            1.0
        );
    }

    bool intersectSphere(Ray ray, Sphere sphere, out float t) {
        vec3 oc = ray.origin - sphere.center;
        float a = dot(ray.direction, ray.direction);
        float b = 2.0 * dot(oc, ray.direction);
        float c = dot(oc, oc) - sphere.radius * sphere.radius;
        float discriminant = b * b - 4.0 * a * c;
        
        if (discriminant < 0.0) {
            return false;
        }
        
        float t1 = (-b - sqrt(discriminant)) / (2.0 * a);
        float t2 = (-b + sqrt(discriminant)) / (2.0 * a);
        
        if (t1 > 0.001) {
            t = t1;
            return true;
        }
        if (t2 > 0.001) {
            t = t2;
            return true;
        }
        
        return false;
    }

    bool intersectCube(Ray ray, out float t, out vec3 normal) {
        vec3 m = 1.0 / ray.direction;
        vec3 n = m * (ray.origin - cube.center);
        vec3 k = abs(m) * cube.size;
        
        vec3 t1 = -n - k;
        vec3 t2 = -n + k;
        
        float tN = max(max(t1.x, t1.y), t1.z);
        float tF = min(min(t2.x, t2.y), t2.z);
        
        if (tN > tF || tF < 0.001) {
            return false;
        }
        
        t = tN > 0.001 ? tN : tF;
        
        if (t == t1.x) normal = vec3(-sign(ray.direction.x), 0.0, 0.0);
        else if (t == t1.y) normal = vec3(0.0, -sign(ray.direction.y), 0.0);
        else if (t == t1.z) normal = vec3(0.0, 0.0, -sign(ray.direction.z));
        else if (t == t2.x) normal = vec3(sign(ray.direction.x), 0.0, 0.0);
        else if (t == t2.y) normal = vec3(0.0, sign(ray.direction.y), 0.0);
        else normal = vec3(0.0, 0.0, sign(ray.direction.z));
        
        return true;
    }

    // Trace objects in the scene for intersections
    HitData traceScene(Ray ray, bool includeLightSphere) {
        HitData closest;
        closest.hit = false;
        closest.t = 1e10;

        // Light sphere intersection
        if (includeLightSphere) {
            float t;
            if (intersectSphere(ray, lightSphere, t)) {
                if (t < closest.t) {
                    closest.hit = true;
                    closest.t = t;
                    closest.point = ray.origin + ray.direction * t;
                    closest.normal = normalize(closest.point - lightSphere.center);
                    closest.color = lightSphere.color;
                    closest.material = lightSphere.material;
                    closest.reflectivity = lightSphere.reflectivity;
                    closest.refractiveIndex = lightSphere.refractiveIndex;
                    closest.frontFace = dot(ray.direction, closest.normal) < 0.0;
                }
            }
        }

        // Cube intersection
        float cube_t;
        vec3 cubeNormal;
        if (intersectCube(ray, cube_t, cubeNormal)) {
            if (cube_t < closest.t) {
                closest.hit = true;
                closest.t = cube_t;
                closest.point = ray.origin + ray.direction * cube_t;
                closest.normal = cubeNormal;
                closest.color = cube.color;
                closest.material = MATERIAL_REFLECTIVE;
                closest.reflectivity = cube.reflectivity;
                closest.refractiveIndex = 1.0;
                closest.frontFace = dot(ray.direction, closest.normal) < 0.0;
            }
        }

        // Sphere intersections
        for (int i = 0; i < 2; i++) {
            float t;
            if (intersectSphere(ray, spheres[i], t)) {
                if (t < closest.t) {
                    closest.hit = true;
                    closest.t = t;
                    closest.point = ray.origin + ray.direction * t;
                    closest.normal = normalize(closest.point - spheres[i].center);
                    closest.color = spheres[i].color;
                    closest.material = spheres[i].material;
                    closest.reflectivity = spheres[i].reflectivity;
                    closest.refractiveIndex = spheres[i].refractiveIndex;
                    closest.frontFace = dot(ray.direction, closest.normal) < 0.0;
                    if (!closest.frontFace) {
                        closest.normal = -closest.normal;
                    }
                }
            }
        }
        return closest;
    }

    // refraction
    vec3 refraction(vec3 I, vec3 N, float eta) {
        float cosI = -dot(N, I);
        float sinT2 = eta * eta * (1.0 - cosI * cosI);
        if (sinT2 > 1.0) {
            return vec3(0.0);
        }
        float cosT = sqrt(1.0 - sinT2);
        return eta * I + (eta * cosI - cosT) * N;
    }

    // Schlick approximation
    float schlick(float cosine, float ref_idx) {
        float r0 = (1.0 - ref_idx) / (1.0 + ref_idx);
        r0 = r0 * r0;
        return r0 + (1.0 - r0) * pow((1.0 - cosine), 5.0);
    }

    vec3 trace(Ray ray, int maxDepth) {
        vec3 color = vec3(0.0);

        //attenuation is optional
        vec3 attenuation = vec3(1.0);

        for (int depth = 0; depth < 8; depth++) {
            if (depth >= maxDepth) break;

            HitData lightHit = traceScene(ray, true);
            
            // If we hit the light directly, return its emission
            if (lightHit.hit && lightHit.color.x > 5.0) {
                color += attenuation * lightHit.color;
                break;
            }

            HitData hit = traceScene(ray, false);

            if (!hit.hit) {
                // Background gradient
                float t = 0.5 * (ray.direction.y + 1.0);
                color += attenuation * mix(vec3(0.05, 0.05, 0.1), vec3(0.3, 0.5, 0.8), t);
                break;
            }

            vec3 lightDir = normalize(lightSphere.center - hit.point);
            float lightDist = length(lightSphere.center - hit.point);

            // Shadow ray
            Ray shadowRay = Ray(hit.point + hit.normal * 0.001, lightDir);
            HitData shadowHit = traceScene(shadowRay, false);
            bool inShadow = shadowHit.hit && shadowHit.t < lightDist;

            if (hit.material == MATERIAL_DIFFUSE) {
                // Phong lighting
                float diff = max(dot(hit.normal, lightDir), 0.0);
                vec3 viewDir = normalize(-ray.direction);
                vec3 reflectDir = reflect(-lightDir, hit.normal);
                float spec = pow(max(dot(viewDir, reflectDir), 0.0), 32.0);
                
                float lightIntensity = 1.0 / (lightDist * lightDist);
                vec3 directLight = inShadow ? vec3(0.0) : 
                    hit.color * lightSphere.color * lightIntensity * (diff * 0.8 + spec * 0.3);
                
                vec3 ambientLight = hit.color * u_ambientStrength;

                vec3 lighting = directLight + ambientLight;
                color += attenuation * lighting;
                break;

            } else if (hit.material == MATERIAL_REFRACTIVE) {
                float eta = hit.frontFace ? (1.0 / hit.refractiveIndex) : hit.refractiveIndex;
                vec3 refracted = refraction(ray.direction, hit.normal, eta);
                bool total_internel_reflectioin = length(refracted) < 0.01;
                
                float cosine = hit.frontFace ? 
                    -dot(ray.direction, hit.normal) : 
                    dot(ray.direction, hit.normal);
                float schlick_value = schlick(cosine, hit.refractiveIndex);
                
                vec3 direction;
                if (total_internel_reflectioin || schlick_value > 0.5) {
                    //direction = reflection ray
                    direction = reflect(ray.direction, hit.normal);
                } else {
                    //direction = refraction ray
                    direction = refracted;
                }

                ray = Ray(hit.point + direction * 0.001, direction);
                attenuation *= hit.color;

            } else {
                // Reflective material
                attenuation *= hit.reflectivity;
                ray = Ray(hit.point + hit.normal * 0.001, reflect(ray.direction, hit.normal));
            }

            if (length(attenuation) < 0.01) break;
        }

        return color;
    }

    void main() {
        initScene(u_time);

        vec2 uv = (gl_FragCoord.xy - 0.5 * u_resolution) / u_resolution.y;

        vec3 origin = vec3(0.0, 0.0, 2.0);
        vec3 direction = normalize(vec3(uv, -1.5));

        Ray ray = Ray(origin, direction);
        vec3 color = trace(ray, u_maxBounces);

        color = pow(color, vec3(1.0 / 2.2));

        fragColor = vec4(color, 1.0);
    }
</script>

<script>
    (function() {
        const canvas = document.getElementById('glCanvas');
        const gl = canvas.getContext('webgl2');

        if (!gl) {
            alert('WebGL 2 not supported in this browser');
            throw new Error('WebGL 2 not supported');
        }

        const vsEditor = document.getElementById('vsEditor');
        const fsEditor = document.getElementById('fsEditor');

        const vertexShaderSource = document.getElementById('vertex-shader').textContent;
        const fragmentShaderSource = document.getElementById('fragment-shader').textContent;

        // Initialize editors with shader source
        vsEditor.value = vertexShaderSource;
        fsEditor.value = fragmentShaderSource;

        let program, positionLocation, resolutionLocation, timeLocation, maxBouncesLocation, ambientStrengthLocation;
        let vao, positionBuffer;

        function createShader(gl, type, source) {
            const shader = gl.createShader(type);
            gl.shaderSource(shader, source);
            gl.compileShader(shader);

            if (!gl.getShaderParameter(shader, gl.COMPILE_STATUS)) {
                throw new Error(gl.getShaderInfoLog(shader));
            }

            return shader;
        }

        function createProgram(gl, vsSource, fsSource) {
            const vs = createShader(gl, gl.VERTEX_SHADER, vsSource);
            const fs = createShader(gl, gl.FRAGMENT_SHADER, fsSource);
            const prog = gl.createProgram();
            gl.attachShader(prog, vs);
            gl.attachShader(prog, fs);
            gl.linkProgram(prog);

            if (!gl.getProgramParameter(prog, gl.LINK_STATUS)) {
                throw new Error(gl.getProgramInfoLog(prog));
            }

            return prog;
        }

        const positions = new Float32Array([
            -1, -1,
            1, -1,
            -1, 1,
            1, 1
        ]);

        // Initialize buffers
        function initBuffers() {
            positionBuffer = gl.createBuffer();
            gl.bindBuffer(gl.ARRAY_BUFFER, positionBuffer);
            gl.bufferData(gl.ARRAY_BUFFER, positions, gl.STATIC_DRAW);

            vao = gl.createVertexArray();
            gl.bindVertexArray(vao);
            gl.bindBuffer(gl.ARRAY_BUFFER, positionBuffer);
            gl.enableVertexAttribArray(positionLocation);
            gl.vertexAttribPointer(positionLocation, 2, gl.FLOAT, false, 0, 0);
            gl.bindVertexArray(null);
        }

        function initShaderProgram() {
            try {
                program = createProgram(gl, vsEditor.value, fsEditor.value);
                gl.useProgram(program);
                positionLocation = gl.getAttribLocation(program, 'position');
                resolutionLocation = gl.getUniformLocation(program, 'u_resolution');
                timeLocation = gl.getUniformLocation(program, 'u_time');
                maxBouncesLocation = gl.getUniformLocation(program, 'u_maxBounces');
                ambientStrengthLocation = gl.getUniformLocation(program, 'u_ambientStrength');
                
                initBuffers();
            } catch (e) {
                console.error(e);
            }
        }

        initShaderProgram();

        // Hot compile when any input in the playground text area
        vsEditor.onkeyup = initShaderProgram;
        fsEditor.onkeyup = initShaderProgram;

        let maxBounces = 4;
        let ambientStrength = 0.25;

        // UI Controls
        const bouncesSlider = document.getElementById('bouncesSlider');
        const bouncesValue = document.getElementById('bouncesValue');
        const ambientSlider = document.getElementById('ambientSlider');
        const ambientValue = document.getElementById('ambientValue');

        bouncesSlider.addEventListener('input', (e) => {
            maxBounces = parseInt(e.target.value);
            bouncesValue.textContent = maxBounces;
        });

        ambientSlider.addEventListener('input', (e) => {
            ambientStrength = parseFloat(e.target.value);
            ambientValue.textContent = ambientStrength.toFixed(2);
        });

        let startTime = Date.now();
        function render() {
            const time = (Date.now() - startTime) / 1000.0;

            gl.viewport(0, 0, canvas.width, canvas.height);
            gl.clearColor(0, 0, 0, 1);
            gl.clear(gl.COLOR_BUFFER_BIT);

            gl.useProgram(program);
            gl.bindVertexArray(vao);

            gl.uniform2f(resolutionLocation, canvas.width, canvas.height);
            gl.uniform1f(timeLocation, time);
            gl.uniform1i(maxBouncesLocation, maxBounces);
            gl.uniform1f(ambientStrengthLocation, ambientStrength);

            gl.drawArrays(gl.TRIANGLE_STRIP, 0, 4);

            requestAnimationFrame(render);
        }

        requestAnimationFrame(render);
    })();
</script>

</body>
</html>